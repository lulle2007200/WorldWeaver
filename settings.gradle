pluginManagement {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        mavenCentral()
        gradlePluginPortal()
    }
}
rootProject.name = 'world-weaver'
//include 'bclib-legacy'

apply from: "module-include.gradle"
ext.knownWoverModules.each { module ->
    include module
}


// #### Custom Settings ####

// Change the next line to disable local lib loading
def allowLocalLibUse = true

//When true, the local lib is also used in commandline builds
def allowLocalLibInConsoleMode = false

def WunderLibPath = '../WunderLib'

// #### Logic ####
def isIDE = properties.containsKey('android.injected.invoked.from.ide')
        || (System.getenv("XPC_SERVICE_NAME") ?: "").contains("intellij")
        || (System.getenv("XPC_SERVICE_NAME") ?: "").contains(".idea")
        || System.getenv("IDEA_INITIAL_DIRECTORY") != null

println "IntelliJ: ${isIDE}"

def WunderLibFolder = new File(WunderLibPath)
if (allowLocalLibUse && (isIDE || allowLocalLibInConsoleMode) && WunderLibFolder.exists()) {
    println "Using local WunderLib from '${WunderLibFolder}'"

    include ':wunderlib'
    project(":wunderlib").projectDir = WunderLibFolder
    project(':wunderlib').buildFileName = './wunderlib-composit.gradle'
}
